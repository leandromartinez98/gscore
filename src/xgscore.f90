!
! G-score calculator
!
! Program xgscore
!
! This program computes the G score given a compact alignment log
! generated with xcompactlog, for the gscore computed for a set
! of models in relation to a set of models generated by another 
! alignment
!
! L. Martinez
! Institute of Chemistry - University of Campinas
! Aug 26, 2016
! http://leandro.iqm.unicamp.br
!
program xgscore

  use types
  use file_operations
  use xcompactlog_data
  implicit none
  integer :: i, j 
  integer :: narg, ioerr
  double precision :: scorecut
  character(len=200) :: record, output

  write(*,"(a)") "#" 
  write(*,"(a)") "# G-score calculator " 
  call title()
  write(*,"(a)") "# XGscore: Compute gscore of a set of models relative to another set." 
  write(*,"(a)") "#" 
  write(*,"(a)") "# L. Martinez - Institute of Chemistry, University of Campinas" 
  write(*,"(a)") "# http://leandro.iqm.unicamp.br" 
  write(*,"(a)") "#" 
  write(*,"(a)") "# Reference:" 
  write(*,"(a)") "# L. Martinez, A. Ferrari, F. C. Gozzo," 
  write(*,"(a)") "# A model evaluation score for ... 2016" 
  write(*,"(a)") "#" 

  narg = iargc()
  if ( narg /= 3 ) then
    write(*,*) ' ERROR: Run with: ./xgscore [compact align log] [score cut] [output file]'
    stop
  end if
  call getarg(1,compactlog)
  call getarg(2,record)
  read(record,*,iostat=ioerr) scorecut
  if ( ioerr /= 0 ) then
    write(*,*) ' ERROR: Could not read score cut from second command line argument. '
    stop
  end if
  call getarg(3,output)

  ! Print the input options

  write(*,"(a,a)") "# Alignment log file (compact form): ", trim(adjustl(compactlog)) 
  write(*,"(a)") "#" 
  write(*,"(a,f12.5)") "# Score similarity cutoff: ", scorecut
  write(*,"(a)") "#" 

  ! Read the compact log file

  call read_xcompactlog(10)

  ! Compute G-score for all models of second list

  write(*,"(a)") "# Computing the G-scores ... "
  do i = 1, nmodels2
    model2(i)%gscore = 0.d0
  end do
  do i = 1, nmodels1
    call progress(i,1,nmodels1)
    do j = 1, nmodels2
      if ( scores(i,j) > scorecut ) then
        model2(j)%gscore = model2(j)%gscore + 1.d0
      end if
    end do
  end do
  call progress(nmodels2,1,nmodels2)
  do i = 1, nmodels2
    model2(i)%gscore = model2(i)%gscore / dble(nmodels2)
  end do

  ! Order models from greater to lower G-scores

  call sort_by_gscore(nmodels2,model2)

  !
  ! Write output file 
  !

  write(*,"(a)") "# Writing output file ... "
  open(10,file=output,action='write')
  write(10,"(a)") "# Output of G-score"
  write(10,"(a,a)") "# Input alignment log: ", trim(adjustl(compactlog))
  if ( score_type == 1 ) then
    write(10,"(a)") "# Score type: GDT_TS"
  end if
  if ( score_type == 2 ) then
    write(10,"(a)") "# Score type: TM-score"
  end if
  write(10,"(a,f12.5)") "# Score cutoff: ", scorecut
  write(10,"(a)") "#"
  write(10,"(a)") "# G-score is -RTln(P) for RT=0.593 kcal/mol (T=298.15)"
  write(10,"(a)") "#"
  write(10,"(a)") "#    G-score     Degree(P)  Model"
  do i = 1, nmodels2
    write(10,"(2(f12.5,tr2),a)") -0.593d0*dlog(model2(i)%gscore+1.d-30), model2(i)%gscore, trim(adjustl(model2(i)%name))
  end do
  close(10)

  write(*,"(a)") "#"
  write(*,"(a,a)") "# Wrote file: ", trim(adjustl(output))
  write(*,"(a)") "#"
  write(*,"(a)") "# Finished. " 

end program xgscore

